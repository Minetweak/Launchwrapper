apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    maven {
        url "https://libraries.minecraft.net/"
    }
    mavenCentral()
}

group = 'io.minetweak'
archivesBaseName = 'launchwrapper'
version = '1.0-SNAPSHOT'
sourceCompatibility = 1.6

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

artifacts {
    archives jar
    archives sourcesJar
}

def repoDir = new File(projectDir, "repo")
repoDir.mkdirs()

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.archives

            repository(url: 'http://nexus.directcode.org/nexus/content/repositories/snapshots/') {
                authentication(userName: project.property('dcUsername'), password: project.property('dcPassword'))
            }

            pom.project {
                description 'Minecraft Launchwrapper'
                url 'http://github.com/Minetweak/Launchwrapper/'
            }
        }
    }

    doLast {
        // Purge all annoying files that arent needed
        repoDir.traverse(type: groovy.io.FileType.FILES, nameFilter: ~/.*\.(xml(?:\.sha1)?|md5)$/) {
            it.delete()
        }
    }
}

clean << {
    repoDir.deleteDir()
}
